apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao' // apply plugin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'//扩展插件，无需findViewById,直接引用布局文件的id即可
apply plugin: 'kotlin-kapt'  //kapt3插件
apply from: 'dependence.gradle'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId "cn.hzjdemo.hellodiary"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // 开启multidex
        multiDexEnabled true

        //无需多版本控制，gradle3.0+需要配置这句话
        flavorDimensions "jacky"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi'//, 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    lintOptions {
        //忽略一些构建信息，降低对错误的检查
        checkReleaseBuilds false
        abortOnError false
    }

    //支持java 8 lambdas
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    signingConfigs {
        release {
            storeFile file(RELEASE_KEYSTORE_FILE)
            storePassword RELEASE_KEYSTORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }

        debug {
            storeFile file(DEBUG_KEYSTORE_FILE)
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
        }

        release {
            //启用混淆代码的功能
            minifyEnabled true
            //压缩对齐生成的apk包
            zipAlignEnabled true
            //指定混淆规则，需要压缩优化的混淆要把proguard-android.txt换成proguard-android.txt
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //指定打release包的签名
            signingConfig signingConfigs.release
            //移除无用的资源文件
            shrinkResources true
        }

    }

    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    repositories {
        flatDir {
            dirs 'libs' //this way we can find the .aar file in libs folder
        }
    }
}

//greendao配置
greendao {
    schemaVersion 1
    daoPackage 'cn.hzjdemo.hellodiary.gen'
    targetGenDir 'src/main/java'
}